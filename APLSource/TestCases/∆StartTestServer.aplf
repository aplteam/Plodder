 r←∆StartTestServer dummy;LXwas;WSIDwas;tempWSID;cl;q;exe;cmd;_
⍝ Starts a separate instance of Dyalog (RunTime) and starts the server within that instance.
⍝ The optional left argument defaults to 0. 1 allows one to RIDE into the running server.
⍝ *** For a RIDE the Conga DLLs must be available as siblings of the workspace
⍝ *** This function changes ⎕LX and ⎕WSID but only temporarily.
 r←1
 ##.RumbaLean.⎕EX'DRC'               ⍝ Delete any leftovers : we need to save this WS
 LXwas←⎕LX
 ⎕LX←'#.Plodder.TestRun 1'
 WSIDwas←⎕WSID
 tempWSID←⎕WSID←'Plodder_Testserver'
 tempWSID←'expand'##.FilesAndDirs.NormalizePath tempWSID
 0 ⎕SAVE ⎕WSID
 ⎕WSID←WSIDwas
 ⎕LX←LXwas
 cl←##.Plodder.GetCommandLineArgs_   ⍝ Current command line
 q←{'"',⍵,'"'}                       ⍝ Quote stuff
 exe←⊃cl                             ⍝ The interpreter
 :If 1
     :If 'W'=⊃⊃#.APLVersion
         :If 0=+/'Dyalogrt.exe'⍷exe  ⍝ Might be already a runtime exe (Ride!)
             exe←(exe↑⍨'Dyalog.exe'{¯1+1⍳⍨⍺⍷⍵}exe),'Dyalogrt.exe' ⍝ Make it runtime exe
         :EndIf
     :Else
         :If 0=+/'/dyalog.rt'⍷exe    ⍝ Might be already a runtime exe (Ride!)
             exe←(exe↑⍨'/dyalog'{¯1+1⍳⍨⍺⍷⍵}exe),'/dyalog.rt' ⍝ Make it runtime exe
         :EndIf
     :EndIf
 :EndIf
 cmd←⊂q exe                          ⍝
 cmd,←⊂q tempWSID                    ⍝ The workspace
 cmd,←⊂'maxws=64MB'
 cmd,←⊂'DYALOG_NOPOPUPS=1'           ⍝ We don't want the dialog box "You have successfully..." nor any other
 cmd,←⊂'OFF=1'                       ⍝ Should finish itself
 :If 0
     cmd,←⊂'-ride=4502'              ⍝ User wants to have a RIDE
 :EndIf
 :If 0
     cmd,←⊂'-x'                      ⍝ Do not execute ⎕LX
 :EndIf
 :If 'W'=⊃⊃#.APLVersion
     _←⎕CMD(1↓∊' ',¨cmd)''           ⍝ Run cmd
 :Else
     _←⎕CMD(1↓∊' ',¨cmd),'&'         ⍝ Run cmd
 :EndIf
 r←0
⍝Done
