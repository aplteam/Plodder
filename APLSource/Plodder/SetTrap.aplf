 TRAP←{force}SetTrap G;folder;appName
⍝ General error trapping in case something goes horribly wrong.
⍝ This does not establish error trapping by default in development, but this can be enforced by passing a 1 as left argument.
⍝ In Runtime error trapping is always established.
⍝ Requires #.ErrrorParms to exist!
 force←{0<⎕NC ⍵:⍎⍵ ⋄ 0}'force'
 :If ¯1=force
     force←~A.IsDevelopment
 :EndIf
 appName←G.AppName
 folder←⊃G.INI.Get'Trap:Folder'
 #.ErrorParms.errorFolder←folder
 'create!'F.CheckPath #.ErrorParms.errorFolder
 :If 'WIN'≡1 ⎕C ##.APLTreeUtils2.GetOperatingSystem ⍬
     #.ErrorParms.windowsEventSource←appName
 :EndIf
 #.ErrorParms.AppName←appName
 TRAP←force ##.HandleError.SetTrap'#.ErrorParms'
⍝Done
